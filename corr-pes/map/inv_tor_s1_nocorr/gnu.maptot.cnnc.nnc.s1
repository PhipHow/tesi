set size square
set grid
set nolabel
set noarrow
set nokey

set xrange [0:180]
set yrange [100:180]
set xtics 0,30,180
set ytics 100,20,180

set view 0,0,1.0,1.0
set nosurface
set contour
set noclabel
set ztics
#set colors classic


set border lc "black" lw 0.25  # thin lines for the tics
set border 0                   # don't draw the normal border
set tics scale 2.0             # longer than usual tics
set obj 1 rectangle from graph 0,0 to graph 1,1
set obj 1 lc "black" lw 1.0    # heavy line for the "border"
set obj 1 fillstyle empty
set grid x y

set table 'tab1'
set cntrparam levels incremental  0.0,0.05,10.0
splot 'risu.map.cnnc.nnc.s1.combi' using 5:3:7
# splot 'risu.mapdian.s1.notrageo' using 5:3:7
unset table

set table 'tab2'
set cntrparam levels incremental  0.0,0.5,10.0
splot 'risu.map.cnnc.nnc.s1.combi' using 5:3:7
# splot 'risu.mapdian.s1.notrageo' using 5:3:7
unset table

set table 'tab3'
set cntrparam levels incremental  0.00,0.20,0.20
splot 'risu.map.cnnc.nnc.s1.combi' using 5:3:($7-$6)
unset table

set label 'A' at 185.0, 114.0 center front
set label 'B' at  -5.0, 128.6 center front
set label 'C' at 185.0, 131.0 center front
set label 'D' at  -5.0, 149.0 center front
set label 'E' at 137.0, 130.5 center front
set label 'F' at 101.8, 121.1 center front
set label 'F' at 104.0, 148.0 center front

set encoding iso_8859_1
set xlabel 'Angolo CNNC, gradi' offset 0,0
set ylabel 'Angolo NNC_1, gradi' offset 0,0
set style textbox opaque noborder


set terminal cairolatex pdf color dashed lw 3
set output "map.invtorss1.nocorr.tex"

plot \
'tab3' u 1:2 with filledcurves lc 5, \
'tab1' u 1:2 with lines lt 1 lc 6 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 7 lw 1.0, \
'tab2' u 1:2:3 every 20 w labels boxed, \
'crit.points.s1' u 1:2 w p pt 7 lc 6 ps 0.3


# set term pslatex color dashed lw 2
# set size 1.0,1.0
# set output 'fig.map1.tex'
# replot

#set term pdf font "Times-Roman,8" color dashed lw 2
#set size 1.0,1.0
#set output 'map.invtorss1.nocorr.pdf'
#replot

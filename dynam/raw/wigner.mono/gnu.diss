set key right top spacing 1.5
set xlabel 'time, fs'
#set ylabel 'energy, eV'
#set xrange [0.0:500.0]
#set yrange [-0.002:0.01]
set grid
set samples 1000
set colors classic

ac = 0.5
tauc = 3000
cis(x) = ac * exp(-x/tauc)
# fit [1000:10000] cis(x) 'azmtraj.pun' using 1:13 via ac,tauc

at = 0.2
taut = 20000
tra(x) = at * exp(-x/taut)
fit [1000:10000] tra(x) 'azmtraj.pun' using 1:9 via at,taut

td = 170
tau1 = 100
tau2 = 1000
w = 0.3
und(x) = x<td ? 1.0 : w*exp(-(x-td)/tau1) + (1-w)*exp(-(x-td)/tau2)
fit und(x) 'azmtraj.pun' using 1:3 via td,tau1,tau2,w

plot \
'azmtraj.pun' u 1:3 title 'undissociated' w l lt 1 lc 1 lw 1, \
und(x) title 'biexponential fit' w l lt 1 lc 7 lw 0.6, \
'azmtraj.pun' u 1:5 title 'first dissociation' w l lt 1 lc 2 lw 1, \
'azmtraj.pun' u 1:7 title 'second dissociation' w l lt 1 lc 3 lw 1, \
'azmtraj.pun' u 1:9 title 'S_0 trans' w l lt 1 lc 4 lw 1, \
'azmtraj.pun' u 1:13 title 'S_0 cis' w l lt 1 lc 5 lw 1

# 'azmtraj.pun' u 1:($9+$13) title 'cis + trans' w l lt 1 lc 6 lw 1, \
# cis(x) notitle w l lt 1 lc 7 lw 0.6, \
# tra(x) notitle w l lt 1 lc 7 lw 0.6


# set term pslatex color dashed lw 3
# set size 1.00,0.80
# set output 'fig.onemode.q.wigner.tex'
# replot

set term pdf font "Times-Roman,10" color dashed lw 2
set size 1.0,1.0
set output 'diss.pdf'
replot

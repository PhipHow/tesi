set key at 330,165 spacing 1.5
set xlabel 'Angolo CNNC, gradi' offset 0,0
set ylabel 'Angolo NNC_1, gradi' offset 0,0
set xrange [80:180]
set yrange [100:180]
#set grid
#set view 0,0,1.0,1.0
set nosurface
set contour
set noclabel
set ztics
set colors classic


set parametric 
set trange [0:250]

#######################################################################
set border lc "black" lw 0.25  # thin lines for the tics
set border 0                   # don't draw the normal border
set tics scale 2.0             # longer than usual tics
set obj 1 rectangle from graph 0,0 to graph 1,1
set obj 1 lc "black" lw 1.3    # heavy line for the "border"
set obj 1 fillstyle empty
set grid x y
#######################################################################


set macros
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.65"
MMARGIN = "set tmargin at screen 0.65; set bmargin at screen 0.40"
BMARGIN = "set tmargin at screen 0.40; set bmargin at screen 0.15"
LMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.55"
RMARGIN = "set lmargin at screen 0.55; set rmargin at screen 0.95"

XSTUFF = "set xtics('0'0,'30'30,'180'180); set xlabel 'Angolo CNNC, gradi'"
YSTUFF = "set ytics('20'20,'100'100,'180'180); set ylabel 'Angolo NNC_1, gradi'"
NOXSTUFF = "set xtics(''0,''30,''180); unset xlabel"
NOYSTUFF = "set ytics(''20,''100,''180);unset ylabel"




set term pdf font "Times-Roman,11" color dashed lw 2
set term pdf size 6.0,9.0
set output 'midtraj.pdf'




set table 'tab1'
set cntrparam levels incremental  0.0,0.05,10.0
splot 'zpe.map.invtors1' using 5:3:7
# splot 'risu.mapdian.s1.notrageo' using 5:3:7
unset table

set table 'tab2'
set cntrparam levels incremental  0.0,0.5,10.0
splot 'zpe.map.invtors1' using 5:3:7
# splot 'risu.mapdian.s1.notrageo' using 5:3:7
unset table

set table 'tab3'
set cntrparam levels incremental  0.00,0.20,0.20
splot 'zpe.map.invtors1' using 5:3:($7-$6)
unset table






set multiplot layout 3,3 rowsfirst


@YSTUFF; @NOXSTUFF; @TMARGIN; @LMARGIN
set ytics('100'100,'20'20,'180'180)
unset ylabel
set label 1 'ZPE300' at graph 0.70,0.9 font "1"
plot \
'azmtraj300.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtraj300.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels


@NOYSTUFF; @TMARGIN; @RMARGIN
set label 1 'ZPE300.mono' at graph 0.60,0.9 font "1"
plot \
'azmtraj300mc.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtraj300mc.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels


@NOXSTUFF; @YSTUFF; @MMARGIN; @LMARGIN
set ytics('100'100,'20'20,'180'180)
set label 1 'ZPE50.mono' at graph 0.60,0.9 font "1"
plot \
'azmtraj50mc.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtraj50mc.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels


set key
@NOYSTUFF; @RMARGIN; @MMARGIN
set label 1 'Wigner' at graph 0.70,0.9 font "1"
plot \
'azmtrajw.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtrajw.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels

unset key
@XSTUFF; @YSTUFF; @LMARGIN; @BMARGIN
set xtics('0'0,'30'30,'180'180)
unset ylabel
set label 1 'Boltzmann' at graph 0.65,0.9 font "1"
plot \
'azmtrajbo.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtrajbo.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels


@XSTUFF; @NOYSTUFF; @RMARGIN; @BMARGIN
set xtics('0'0,'30'30,'180'180)
unset ylabel
set label 1 'Wigner.red' at graph 0.60,0.9 font "1"
plot \
'azmtrajrw.pun' u 29:($1 < 250 ? $25 : 1/0):1 with lines lt 1 lc 2 lw 1.2, \
'azmtrajrw.pun' u 29:($1 < 250 ? $27 : 1/0):1 with lines lt 1 lc 4 lw 1.2,\
'tab3' u 1:2 with filledcurves lc 6, \
'tab1' u 1:2 with lines lt 1 lc 3 lw 0.5, \
'tab2' u 1:2 with lines lt 1 lc 1 lw 1.2, \
'tab2' u 1:2:3 every 20 w labels


unset multiplot

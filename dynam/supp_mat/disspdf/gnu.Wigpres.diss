set key at 9700,0.60 spacing 1.2
set xlabel 'time, fs'
set ylabel 'Population'
#set xrange [0.0:500.0]
#set yrange [-0.002:0.01]
set grid
set samples 1000


set border lc "black" lw 0.25  # thin lines for the tics
set border 0                   # don't draw the normal border
set tics scale 2.0             # longer than usual tics
set obj 1 rectangle from graph 0,0 to graph 1,1
set obj 1 lc "black" lw 1.0    # heavy line for the "border"
set obj 1 fillstyle empty
set grid x y


set macros
TMARGIN = "set tmargin at screen 0.95; set bmargin at screen 0.525"
BMARGIN = "set tmargin at screen 0.525; set bmargin at screen 0.10"
LMARGIN = "set lmargin at screen 0.10; set rmargin at screen 0.525"
RMARGIN = "set lmargin at screen 0.525; set rmargin at screen 0.95"

XSTUFF = "set xtics('1000'1000,'3000'3000,'5000'5000,'7000'7000,'9000'9000,); set xlabel 'Tempo, fs'"
YSTUFF = "set ytics('0'0,'0.2'0.2,'0.4'0.4,'0.6'0.6,'0.8'0.8,'0'1); set ylabel 'Popolazione'"
NOXSTUFF = "set xtics(''1000,''3000,''5000,''7000,''9000); unset xlabel"
NOYSTUFF = "set ytics(''0,''0.2,''0.4,''0.6,''0.8,''1);unset ylabel"



set terminal cairolatex pdf size 7,3 lw 3
set output "presdissWig.tex"


#set term pdf font "Times-Roman,11" color dashed lw 2
#set term pdf size 7,3
#set output 'presdissWig.pdf'

set multiplot layout 1,2 rowsfirst


@XSTUFF; @YSTUFF; @LMARGIN; 
set ytics('0'0,'0.2'0.2,'0.4'0.4,'0.6'0.6,'0.8'0.8,'1'1)
set label 1 'Wigner' at graph 0.65,0.9 font "1"
set key
plot \
'azmtrajw.pun' u 1:3 title 'Indissociate' w l lt 1 lc 8 lw 1, \
'azmtrajw.pun' u 1:5 title 'Prima dissociazione' w l lt 1 lc 7 lw 1, \
'azmtrajw.pun' u 1:7 title 'Seconda dissociazione' w l lt 1 lc 6 lw 1, \
'azmtrajw.pun' u 1:9 title 'S_0 trans' w l lt 1 lc 1 lw 1, \
'azmtrajw.pun' u 1:13 title 'S_0 cis' w l lt 1 lc 2 lw 1


@NOYSTUFF; @XSTUFF; @RMARGIN
unset key
unset ylabel
set label 1 'Wigner.rid' at graph 0.70,0.9 font "1"
plot \
'azmtrajrw.pun' u 1:3 title 'Indissociate' w l lt 1 lc 8 lw 1, \
'azmtrajrw.pun' u 1:5 title 'Prima dissociazione' w l lt 1 lc 7 lw 1, \
'azmtrajrw.pun' u 1:7 title 'Seconda dissociazione' w l lt 1 lc 6 lw 1, \
'azmtrajrw.pun' u 1:9 title 'S_0 trans' w l lt 1 lc 1 lw 1, \
'azmtrajrw.pun' u 1:13 title 'S_0 cis' w l lt 1 lc 2 lw 1


unset multiplot



